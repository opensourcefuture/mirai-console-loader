plugins {
    id 'java'
}

group 'org.itxtech'
version '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.mozilla:rhino:1.7.13")
    implementation("commons-cli:commons-cli:1.4")
    implementation("com.google.code.gson:gson:2.8.7")
    compileOnly("org.fusesource.jansi:jansi:2.3.3")
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

jar {
    manifest {
        attributes "Main-Class": "org.itxtech.mcl.Loader"
        attributes "Version": project.version + "-" + getGitHash()
        attributes "Launcher-Agent-Class": "org.itxtech.mcl.Agent"
        attributes "Agent-Class": "org.itxtech.mcl.Agent"
        attributes "Premain-Class": "org.itxtech.mcl.Agent"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
